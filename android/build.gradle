buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['YamapPlus_' + name]
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault("kotlinVersion")}"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["YamapPlus_" + name]).toInteger()
}

android {
  namespace = "ru.vvdev.yamap"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
                "generated",
                'src/newarch'
        ]
      } else {
        java.srcDirs += [
                'src/oldarch'
        ]
      }
    }
  }
}

repositories {
  mavenCentral()
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def play_services_location_version = getExtOrDefault("playServicesLocationVersion")
def maps_mobile_version = getExtOrDefault("mapsMobileVersion")

println "kotlin_version=" + kotlin_version
println "play_services_location_version=" + play_services_location_version
println "maps_mobile_version=" + maps_mobile_version

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation "com.google.android.gms:play-services-location:$play_services_location_version"
  implementation "com.yandex.android:maps.mobile:$maps_mobile_version"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "RNYamapPlus"
    codegenJavaPackageName = "ru.vvdev.yamap"
  }
}
